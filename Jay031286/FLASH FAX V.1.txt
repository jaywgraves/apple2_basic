 10  REM  *****************
 20  REM  ***           ***
 30  REM  *** FLASH FAX ***
 40  REM  ***           ***
 50  REM  *****************
 60  REM 
 70  REM  *** JAY GRAVES
 80  REM  *** ORIGINAL STARTED 3/16/86 - FINISHED 'SUMMER' 86
 90  REM  *** ENHANCED 3/20/87
 100  REM 
 110  REM  *** MULTIPLICATION QUIZ FOR LEARNING ***
 120  REM  
 130  REM  *** VARIABLES ***
 140  REM 
 150  REM  *** PROGRAM VARIABLES
 160  REM  
 170  REM  *** N1 & N2 = 1ST AND 2ND NUMBERS
 180  REM  *** N3 = CORRECT ANSWER
 190  REM  *** A = ANSWER GIVEN
 200  REM  *** T = TIME CHOICE
 210  REM  *** T1 = TIME VARIABLE
 220  REM  *** Q$ = DECISION VARIABLE
 230  REM  *** I = LOOP & COUNTER VARIABLE
 240  REM  *** WP = WRONG PROBLEM COUNTER
 250  REM  *** CP = CORRECT PROBLEM COUNTER
 260  REM  *** TP = TOTAL PROBLEM COUNTER
 270  REM  *** P = PERCENTAGE VARIABLE
 280  REM  
 290  REM  *** FILE VARIABLES
 300  REM  
 310  REM  *** F$ = FILE NAME
 320  REM  *** N$ = NAME VARIABLE
 330  REM  
 340  REM  *** GRAPHICS PAGE
 341  REM 
 342  PR# 0
 350  HOME : GR : COLOR= 15
 360  FOR I = 0 TO 39: VLIN 0,39 AT I: NEXT I
 370  REM  *** 'F' IN FLASH
 380  COLOR= 2
 390  HLIN 3,7 AT 3: HLIN 3,7 AT 4
 400  VLIN 5,14 AT 3: VLIN 5,14 AT 4: VLIN 8,9 AT 5: VLIN 8,9 AT 6
 410  REM  *** 'L' IN FLASH
 420  VLIN 3,14 AT 9: VLIN 3,14 AT 10
 430  HLIN 11,13 AT 13: HLIN 11,13 AT 14
 440  REM  *** 'A' IN FLASH
 450  VLIN 5,14 AT 15: VLIN 4,14 AT 16: VLIN 4,14 AT 19: VLIN 5,14 AT 20
 460  HLIN 17,18 AT 3: HLIN 17,18 AT 4: HLIN 17,18 AT 8: HLIN 17,18 AT 9
 470  REM  *** 'S' IN FLASH
 480  HLIN 22,26 AT 3: HLIN 22,26 AT 4: HLIN 22,26 AT 8: HLIN 22,26 AT 9: HLIN 22,26 AT 13: HLIN 22,26 AT 14
 490  VLIN 5,7 AT 22: VLIN 5,7 AT 23: VLIN 10,12 AT 25: VLIN 10,12 AT 26
 500  REM  *** 'H' IN FLASH
 510  VLIN 3,14 AT 28: VLIN 3,14 AT 29: VLIN 3,14 AT 32: VLIN 3,14 AT 33
 520  VLIN 8,9 AT 30: VLIN 8,9 AT 31
 530  REM  *** 'F' IN FAX
 540  HLIN 3,7 AT 20: HLIN 3,7 AT 21
 550  VLIN 22,31 AT 3: VLIN 22,31 AT 4: VLIN 25,26 AT 5: VLIN 25,26 AT 6
 560  REM  *** 'A' IN FAX
 570  VLIN 22,31 AT 9: VLIN 21,31 AT 10: VLIN 21,31 AT 13: VLIN 22,31 AT 14
 580  HLIN 11,12 AT 20: HLIN 11,12 AT 21: HLIN 11,12 AT 25: HLIN 11,12 AT 26
 590  REM  *** 'X' IN FAX
 600  VLIN 20,24 AT 16: VLIN 20,24 AT 17: VLIN 20,24 AT 20: VLIN 20,24 AT 21
 610  VLIN 24,27 AT 18: VLIN 24,27 AT 19
 620  VLIN 27,31 AT 16: VLIN 27,31 AT 17: VLIN 27,31 AT 20: VLIN 27,31 AT 21
 630  REM  *** PROBLEM
 640  COLOR= 0
 650  REM  *** '3'
 660  VLIN 16,22 AT 33: HLIN 30,33 AT 16: HLIN 30,33 AT 19: HLIN 30,33 AT 22
 670  REM  *** '6'
 680  VLIN 24,30 AT 30: VLIN 27,30 AT 33: HLIN 31,33 AT 24: HLIN 31,33 AT 27: HLIN 31,33 AT 30
 690  REM  *** 'x'
 700  PLOT 26,28: PLOT 27,29: PLOT 26,30: PLOT 28,28: PLOT 28,30
 710  REM  *** '===================='
 720  HLIN 26,35 AT 32
 730  VTAB 22: HTAB 15: PRINT "FLASH FAX"
 740  HTAB 8: PRINT "PROGRAMMED BY JAY GRAVES"
 750  FOR I = 1 TO 3000: NEXT I
 751  REM 
 760  REM  *** TITLE PAGE
 770  TEXT : HOME 
 780  VTAB (5): PRINT "This program will quiz you on multipli-"
 790  PRINT "cation facts.  "
 800  VTAB (9): PRINT "You must type in the correct answer be-"
 810  PRINT "fore time runs out."
 820  VTAB (12): PRINT "You can chose the amount of time you"
 830  PRINT "need."
 840  VTAB (22): FLASH : PRINT "PRESS ANY KEY TO CONTINUE"
 850  GET A$: NORMAL : HOME 
 860  HOME 
 870  VTAB (5): PRINT "AMOUNT OF TIME   1) 10 SECONDS"
 880  HTAB (18): PRINT "2) 15 SECONDS
 890  HTAB (18): PRINT "3) 25 SECONDS"
 900  VTAB (20): PRINT "======================================="
 910  VTAB (22): PRINT "CHOSE THE TIME YOU NEED  ";
 920  GET T
 930  IF T <  = 3 THEN 990
 940  HOME 
 950  VTAB (12): PRINT "THAT NUMBER IS TOO LARGE"
 960  PRINT "PLEASE TRY AGAIN"
 970  FOR I = 1 TO 3000: NEXT I
 980  GOTO 860
 990  ON T GOTO 1020,1070,1120
 1010  REM 
 1020  VTAB (5): HTAB (18): FLASH : PRINT "1) 10 SECONDS": NORMAL 
 1030  VTAB (22): PRINT "Is this the correct amount of time?  ": PRINT "ENTER (Y/N) ": GET Q$
 1040  IF Q$ = "Y" THEN 1170
 1050  VTAB (22): PRINT "                                       "
 1060  GOTO 860
 1070  VTAB (6): HTAB (18): FLASH : PRINT "2) 15 SECONDS": NORMAL 
 1080  VTAB (22): PRINT "Is this the correct amount of time?  ": PRINT "ENTER (Y/N) ": GET Q$
 1090  IF Q$ = "Y" THEN 1170
 1100  VTAB (22): PRINT "                                       "
 1110  GOTO 860
 1120  VTAB (7): HTAB (18): FLASH : PRINT "3) 25 SECONDS": NORMAL 
 1130  VTAB (22): PRINT "Is this the correct amount of time?  ": PRINT "ENTER (Y/N) ": GET Q$
 1140  IF Q$ = "Y" THEN 1170
 1150  VTAB (22): PRINT "                                       "
 1160  GOTO 860
 1170  HOME 
 1180  VTAB (10): PRINT "As soon as the problem appears on the "
 1190  PRINT "on the screen your time begins."
 1200  VTAB (15): PRINT "Press any key when you are ready to "
 1210  PRINT "begin."
 1220  GET A$
 1230  IF T = 1 THEN X = 540
 1240  IF T = 2 THEN X = 800
 1250  IF T = 3 THEN X = 1450
 1260 A = 1:B = 0
 1270  HOME 
 1280 TT = TT + 1
 1290 A1 = A:B1 = B
 1300 A =  INT ( RND (1) * 10) + 1
 1310 B =  INT ( RND (1) * 10) + 1
 1320  IF A1 = A AND B1 = B THEN 1300
 1330 A$ =  STR$ (A)
 1340 B$ =  STR$ (B)
 1350 C$ =  STR$ (C)
 1360 C = A * B
 1370  IF  LEN (A$) = 1 THEN H1 = 21
 1380  IF  LEN (A$) = 2 THEN H1 = 20
 1390  IF  LEN (B$) = 1 THEN H2 = 21
 1400  IF  LEN (B$) = 2 THEN H2 = 20
 1410  IF  LEN (C$) = 1 THEN H3 = 21
 1420  IF  LEN (C$) = 2 THEN H3 = 20
 1430  IF  LEN (C$) = 3 THEN H3 = 19
 1440  VTAB (12): HTAB (H1): PRINT A
 1450  VTAB (13): HTAB (18): PRINT "x"
 1460  VTAB (13): HTAB (H2): PRINT B
 1470  VTAB (14): HTAB (18): PRINT "____"
 1480  VTAB 22: PRINT "Press <RETURN> TO STOP TIMER"
 1490  FOR I = 1 TO X
 1500  IF  PEEK ( - 16384) < 128 THEN 1530
 1510  POKE  - 16368,0
 1520  GOTO 1550
 1530  NEXT I
 1540  GOTO 1750
 1550  VTAB 22: PRINT "ENTER YOUR ANSWER                         "
 1560  VTAB (16): HTAB (H3): INPUT "";D
 1570  IF D = C THEN 1670
 1580  REM  *** WRONG ANSWER
 1590 W = W + 1
 1600  VTAB 22: PRINT "I'M SORRY.  YOUR ANSWER WAS INCORRECT"
 1610  PRINT "THE CORRECT ANSWER WAS "C"."
 1620  PR# 0: FLASH : VTAB 16: HTAB (H3): PRINT C: NORMAL 
 1630  FOR I = 1 TO 2000: NEXT I
 1640  VTAB 22: PRINT "DO YOU WANT ANOTHER PROBLEM? (Y/N)     ": PRINT "                              ": GET Q$
 1650  IF Q$ = "Y" THEN 1270
 1660  GOTO 1850
 1670  REM  *** CORRECT ANSWER
 1680 CC = CC + 1
 1690  PR# 0
 1700  VTAB 22: FLASH : PRINT "THAT'S CORRECT!!! ": NORMAL 
 1710  FOR I = 1 TO 2000: NEXT I
 1720  VTAB 22: PRINT "DO YOU WANT ANOTHER PROBLEM? (Y/N)           ": GET Q$
 1730  IF Q$ = "Y" THEN 1270
 1740  GOTO 1850
 1750  REM  *** TIME RUNS OUT  
 1760 W = W + 1
 1770  VTAB 22: PRINT "I'M SORRY.  YOUR TIME HAS RUN OUT.    "
 1780  PRINT "THE CORRECT ANSWER WAS "C"."
 1790  PR# 0: FLASH : VTAB 16: HTAB (H3): PRINT C: NORMAL 
 1800  FOR I = 1 TO 2000: NEXT I
 1810  VTAB 23: PRINT "                                       "
 1820  VTAB 22: PRINT "DO YOU WANT ANOTHER PROBLEM (Y/N)                   ": GET Q$
 1830  IF Q$ = "Y" THEN 1270
 1840  GOTO 1850
 1850  REM  *** END ROUTINE
 1860  HOME : VTAB 5: PRINT "TOTAL NUMBER OF PROBLEMS: ";TT
 1870  PRINT : PRINT "NUMBER CORRECT: ";CC
 1880  PRINT : PRINT "NUMBER MISSED:  ";W
 1890 P = CC / TT
 1900  PRINT : PRINT "PERCENTAGE:  ";P
 1910  PRINT : PRINT : PRINT "THANKS FOR PLAYING"
