 10  REM  *** RA-INVENTORY
 20  REM  *** JAY GRAVES
 30  REM  *** 3/2/87
 40  HOME :D$ =  CHR$ (4)
 50  DIM N$(200),P$(200),Q$(200)
 60  REM  *** MENU
 70  HOME 
 80  VTAB 3: PRINT "ENTER CHOICE:"
 90  VTAB 7: HTAB 7: PRINT "1) START A FILE"
 100  VTAB 9: HTAB 7: PRINT "2) ADD TO A FILE"
 110  VTAB 11: HTAB 7: PRINT "3) DELETE A FILE"
 120  VTAB 13: HTAB 7: PRINT "4) LOOK AT A FILE"
 130  VTAB 15: HTAB 7: PRINT "5) CHANGE A RECORD"
 131  VTAB 17: HTAB 7: PRINT "6) TERMINATE PROGRAM"
 140  GET X: IF X < 1 OR X > 6 THEN 60
 150  ON X GOTO 160,340,590,700,880,1090
 160  REM  *** START A FILE
 170  HOME 
 180  VTAB 5: PRINT "START A FILE"
 190  VTAB 8: INPUT "FILE NAME: ";F$
 200  VTAB 10: INPUT "NUMBER OF RECORDS: ";N
 210  HOME : FOR I = 1 TO N
 220  INPUT "ITEM: ";N$(I)
 221  INPUT "PRICE: ";P$(I)
 222  INPUT "QUANTITY: ";Q$(I)
 230  PRINT 
 240  NEXT I
 250  HOME 
 260  PRINT D$;"OPEN "F$",L20"
 270  PRINT D$;"WRITE "F$",R0"
 280  PRINT N
 290  FOR I = 1 TO N
 291  PRINT D$;"WRITE "F$",R"I
 300  PRINT N$(I)
 301  PRINT P$(I)
 302  PRINT Q$(I)
 310  NEXT I
 320  PRINT D$;"CLOSE ";F$
 330  GOTO 60
 340  REM  *** ADD WORDS
 350  HOME : VTAB 5: PRINT "ADD WORDS TO FILE"
 360  VTAB 8: INPUT "WHICH FILE: ";F$
 370  HOME 
 380  PRINT D$;"OPEN "F$",L20"
 390  PRINT D$;"READ "F$",R0"
 400  INPUT N
 410  PRINT D$;"CLOSE ";F$
 420  HOME 
 430  VTAB 5: PRINT "THIS FILE HAS BEEN RETRIEVED"
 440  VTAB 8: INPUT "ADD HOW MANY WORDS: ";A
 450  HOME 
 460  FOR I = N + 1 TO N + A
 470  INPUT "ITEM: ";N$(I)
 471  INPUT "PRICE: ";P$(I)
 472  INPUT "QUANTITY: ";Q$(I)
 473  PRINT 
 480  NEXT I
 490 N = N + A
 500  HOME 
 510  PRINT D$;"OPEN "F$",L20"
 520  PRINT D$;"WRITE "F$",R0"
 530  PRINT N
 540  FOR I = 1 TO N
 541  PRINT D$;"WRITE "F$",R"I
 550  PRINT N$(I)
 551  PRINT P$(I)
 552  PRINT Q$(I)
 560  NEXT I
 570  PRINT D$;"CLOSE ";F$
 580  GOTO 60
 590  REM  *** DELETE A FILE
 600  HOME 
 610  VTAB 5: PRINT "DELETE A FILE"
 620  VTAB 8: INPUT "ENTER FILE TO BE DELETED: ";F$
 630  VTAB 12: PRINT "THIS FILE IS ABOUT TO BE PERMANENTLY": PRINT "REMOVED FROM THE DISK."
 640  VTAB 14: INPUT "DO YOU WANT THIS TO HAPPEN? (Y/N) ";Q$
 650  IF Q$ = "N" OR Q$ = "NO" THEN 60
 660  HOME 
 670  VTAB 12: HTAB 14: FLASH : PRINT "SAY GOODBYE": NORMAL 
 680  PRINT D$;"DELETE ";F$
 690  GOTO 60
 700  REM  *** LOOK AT THE FILE
 710  HOME 
 720  VTAB 5: PRINT "LOOK AT A FILE"
 730  VTAB 8: INPUT "WHICH FILE: ";F$
 740  PRINT D$;"OPEN "F$",L20"
 750  PRINT D$;"READ "F$",R0"
 760  INPUT N
 770  FOR I = 1 TO N
 771  PRINT D$;"READ "F$",R"I
 780  INPUT N$(I)
 781  INPUT P$(I)
 782  INPUT Q$(I)
 790  NEXT I
 800  PRINT D$;"CLOSE ";F$
 810  HOME 
 820  PRINT "FILE: ";F$
 830  PRINT : PRINT 
 831  PRINT "ITEM";: HTAB 20: PRINT "PRICE";: HTAB 33: PRINT "QUANTITY"
 832  PRINT "----------------------------------------"
 840  FOR I = 1 TO N
 845  IF N$(I) = "EMPTY" THEN 860
 850  PRINT N$(I);: HTAB 20: PRINT P$(I);: HTAB 35: PRINT Q$(I)
 860  NEXT I
 870  VTAB 22: PRINT "PRESS ANY KEY TO CONTINUE": GET A$: GOTO 60
 880  REM  *** CHANGE OR DELETE A RECORD
 890  HOME 
 900  VTAB 5: PRINT "CHANGE OR DELETE A RECORD"
 910  VTAB 8: INPUT "FILE NAME: ";F$
 915  VTAB 10: INPUT "RECORD NUMBER: ";E
 920  HOME 
 930  PRINT D$;"OPEN "F$",L20"
 940  PRINT D$;"READ "F$",R0"
 941  INPUT N
 942  PRINT D$;"CLOSE "F$
 950  IF E > N THEN  HOME : VTAB 10: PRINT "THERE IS NO SUCH RECORD": FOR I = 1 TO 2000: NEXT I: GOTO 880
 960  HOME 
 970  VTAB 12: PRINT "<C>HANGE THE RECORD"
 980  VTAB 15: PRINT "<D>ELETE THE RECORD"
 990  VTAB 5: INPUT "CHOSE: ";Z$
 1000  IF Z$ = "D" THEN N$(E) = "EMPTY": GOTO 1069
 1010  IF Z$ = "C" THEN 1030
 1020  GOTO 960
 1030  HOME : VTAB 5: PRINT "INPUT NEW DATA"
 1040  VTAB 8: INPUT "ITEM: ";N$(E)
 1050  INPUT "PRICE: ";P$(E)
 1060  INPUT "QUANTITY: ";Q$(E)
 1069  PRINT D$;"OPEN "F$"L20"
 1070  PRINT D$;"WRITE "F$",R"E
 1071  PRINT N$(E)
 1072  PRINT P$(E)
 1073  PRINT Q$(E)
 1080  PRINT D$;"CLOSE "F$: GOTO 60
 1090  REM  *** END
 1100  HOME : END 
