 10  REM  *** ALPHA CITY-ZIP
 20  REM  *** JAY GRAVES
 30  REM  *** 2/9/87
 40  HOME 
 45  READ N
 50  DIM N$(N),Z$(N)
 60  FOR I = 1 TO N
 70  READ N$(I)
 75  READ Z$(I)
 80  NEXT I
 90  FOR Q = 1 TO N - 1
 100  FOR I = 1 TO N - 1
 110  IF N$(I) <  = N$(I + 1) THEN 150
 120 T$ = N$(I)
 121 C$ = Z$(I)
 130 N$(I) = N$(I + 1)
 131 Z$(I) = Z$(I + 1)
 140 N$(I + 1) = T$
 141 Z$(I + 1) = C$
 150  NEXT I
 160  NEXT Q
 170  PRINT "CITY";: HTAB 20: PRINT "ZIP"
 180  PRINT "======================"
 190  FOR I = 1 TO N
 200  PRINT N$(I);: HTAB 18: PRINT Z$(I)
 210  NEXT I
 215  DATA  7
 220  DATA  DENVER,89076,DALLAS,45654,CHICAGO,39173,NEW YORK,80112,HEALY,67850,SACREMENTO,45231,JUNEAU,87654
 230  END 
