 10  REM  *** SF-MENU
 20  REM  *** JAY GRAVES
 30  REM  *** 2/23/87
 40  HOME :D$ =  CHR$ (4)
 50  DIM N$(200)
 60  REM  *** MENU
 70  HOME 
 80  VTAB 3: PRINT "ENTER CHOICE:"
 90  VTAB 7: HTAB 7: PRINT "1) START A FILE"
 100  VTAB 9: HTAB 7: PRINT "2) ADD TO A FILE"
 110  VTAB 11: HTAB 7: PRINT "3) DELETE A FILE"
 120  VTAB 13: HTAB 7: PRINT "4) LOOK AT A FILE"
 130  VTAB 15: HTAB 7: PRINT "5) TERMINATE PROGRAM"
 140  GET X: IF X < 1 OR X > 5 THEN 60
 150  ON X GOTO 160,340,590,700,880
 160  REM  *** START A FILE
 170  HOME 
 180  VTAB 5: PRINT "START A FILE"
 190  VTAB 8: INPUT "FILE NAME: ";F$
 200  VTAB 10: INPUT "NUMBER OF RECORDS: ";N
 210  HOME : FOR I = 1 TO N
 220  INPUT "WORD: ";N$(I)
 230  PRINT 
 240  NEXT I
 250  HOME 
 260  PRINT D$;"OPEN ";F$
 270  PRINT D$;"WRITE ";F$
 280  PRINT N
 290  FOR I = 1 TO N
 300  PRINT N$(I)
 310  NEXT I
 320  PRINT D$;"CLOSE ";F$
 330  GOTO 60
 340  REM  *** ADD WORDS
 350  HOME : VTAB 5: PRINT "ADD WORDS TO FILE"
 360  VTAB 8: INPUT "WHICH FILE: ";F$
 370  HOME 
 380  PRINT D$;"OPEN ";F$
 390  PRINT D$;"READ ";F$
 400  INPUT N
 410  PRINT D$;"CLOSE ";F$
 420  HOME 
 430  VTAB 5: PRINT "THIS FILE HAS BEEN RETRIEVED"
 440  VTAB 8: INPUT "ADD HOW MANY WORDS: ";A
 450  HOME 
 460  FOR I = N + 1 TO N + A
 470  INPUT "WORD: ";N$(I)
 480  NEXT I
 490 N = N + A
 500  HOME 
 510  PRINT D$;"OPEN ";F$
 520  PRINT D$;"WRITE ";F$
 530  PRINT N
 540  FOR I = 1 TO N
 550  PRINT N$(I)
 560  NEXT I
 570  PRINT D$;"CLOSE ";F$
 580  GOTO 60
 590  REM  *** DELETE A FILE
 600  HOME 
 610  VTAB 5: PRINT "DELETE A FILE"
 620  VTAB 8: INPUT "ENTER FILE TO BE DELETED: ";F$
 630  VTAB 12: PRINT "THIS FILE IS ABOUT TO BE PERMANENTLY": PRINT "REMOVED FROM THE DISK."
 640  VTAB 14: INPUT "DO YOU WANT THIS TO HAPPEN? (Y/N) ";Q$
 650  IF Q$ = "N" OR Q$ = "NO" THEN 60
 660  HOME 
 670  VTAB 12: HTAB 14: FLASH : PRINT "SAY GOODBYE": NORMAL 
 680  PRINT D$;"DELETE ";F$
 690  GOTO 60
 700  REM  *** LOOK AT THE FILE
 710  HOME 
 720  VTAB 5: PRINT "LOOK AT A FILE"
 730  VTAB 8: INPUT "WHICH FILE: ";F$
 740  PRINT D$;"OPEN ";F$
 750  PRINT D$;"READ ";F$
 760  INPUT N
 770  FOR I = 1 TO N
 780  INPUT N$(I)
 790  NEXT I
 800  PRINT D$;"CLOSE ";F$
 810  HOME 
 820  PRINT "FILE: ";F$
 830  PRINT : PRINT 
 840  FOR I = 1 TO N
 850  PRINT N$(I)
 860  NEXT I
 870  VTAB 22: PRINT "PRESS ANY KEY TO CONTINUE": GET A$: GOTO 60
 880  REM  *** END PROGRAM
 890  HOME : END 
