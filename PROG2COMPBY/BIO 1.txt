 12  DIM CT$(40): GOSUB 5034
 20  DIM O$(51)
 90  DIM BIRTH$(100)
 100  DIM T$(8)
 120  DIM F(12),J(2),Z$(20)
 130  READ T$(1),T$(2),T$(3),T$(4),T$(5),T$(6),T$(7),T$(8),K
 140  DATA "SUN","MON","TUE","WED","THU","FRI","SAT","  ",6.28318
 150  READ F(1),F(2),F(3),F(4),F(5),F(6),F(7),F(8),F(9),F(10),F(11),F(12)
 160  DATA 31,28,31,30,31,30,31,31,30,31,30,31
 200  HOME 
 202 CT$ = "---------": GOSUB 5040:CT$ = "IMPORTANT": GOSUB 5040:CT$ = "---------": GOSUB 5040
 205 CT$ = "BE SURE THE PRINTER IS ON.": PRINT : GOSUB 5040: PRINT : PRINT 
 208  PRINT "   IF YOUR PRINTER IS NOT IN SLOT 1,       CHANGE PROGRAM LINES 411 AND 417.": PRINT : PRINT : PRINT 
 210  INPUT "YOUR NAME PLEASE? : ";Z$(N + 1)
 215  PRINT "ENTER DATES IN THIS FORMAT MM,DD,YYYY"
 220  LET N = N + 1
 225  PRINT 
 230  PRINT  TAB( 15);"BIRTHDATE:";
 235  INPUT M1,D1,Y1
 238 M1$ =  STR$ (M1):D1$ =  STR$ (D1):Y1$ =  STR$ (Y1):BIRTH$(N) = M1$ + "/" + D1$ + "/" + Y1$
 240  IF Y1 > 99 THEN 255
 245 Y1 = Y1 + 1900
 250  PRINT M1;"/";D1;"/";Y1
 255 M2 = M1:D2 = D1:Y2 = Y1
 260  GOSUB 700
 265 P1 = P2
 270  PRINT "STARTING DATE FOR CHART:";
 275  INPUT M2,D2,Y2
 280  IF Y2 > 99 THEN 295
 285 Y2 = Y2 + 1900
 290  PRINT M2;"/";D2;"/";Y2
 295  GOSUB 700
 297  PRINT "LENGTH OF CHART IN DAYS:";
 298  INPUT L
 300  REM  CALK OFFSET,INCLUDE LEAP YEARS
 305 X = M1
 310  GOSUB 800
 315 J1 = J2 + D1 + Y1 * 365
 320  IF J1 < 639723 THEN P1 = 8
 325 X = M2
 330  GOSUB 800
 335 J2 = J2 + D2 + Y2 * 365
 340  IF J2 < 639723 THEN P2 = 8
 345 N1 = Y2 - .1
 346 O = J2 - J1 +  INT (N1 / 4) -  INT (Y1 / 4) -  INT (N1 / 100) +  INT (Y1 / 100) +  INT (N1 / 400) -  INT (Y1 / 400)
 350  IF M1 > 2 THEN 370
 355 X = Y1
 360  GOSUB 1000
 365 O = O + X
 370  IF Y2 / 4 -  INT (Y2 / 4) <  > 0 THEN 400
 375 X = Y2
 380  GOSUB 1000
 385 O = O + X
 390 F(2) = F(2) + X
 400  REM  PRINT CHART HEADER
 401  HOME : VTAB 9: PRINT " MAKE SURE PRINTER IS ON AND SELECTED,   PRESS <P> TO PRINT OR <ESC> TO EXIT     BACK TO THE SELECT-A-PROGRAM MENU."
 402  PRINT : PRINT "          WHICH?   (P/ESC)";
 403  GET A$: IF A$ <  > "P" AND A$ <  >  CHR$ (27) THEN 403
 404  IF A$ =  CHR$ (27) THEN 9999
 405  FOR I = 1 TO 5
 410  PRINT 
 415  NEXT I
 416  POKE 1785,72
 417  PR# 1
 420  PRINT ;"          ": PRINT ;"BIORHYTHM CHART FOR  ";Z$(N)
 425  PRINT "FOR ";L;" DAYS"
 430  PRINT  TAB( 28);"BORN ON   ";T$(P1);"  ";M1;"/";D1;"/";Y1
 435  PRINT  TAB( 28);"BEGINNING ";T$(P2);" ";M2;"/";D2;"/";Y2
 440  PRINT 
 445  PRINT  TAB( 28);"P=PHYSICAL       (23 DAYS)"
 450  PRINT  TAB( 28);"E=EMOTIONAL      (28 DAYS)"
 455  PRINT  TAB( 28);"I=INTELLECTUAL   (33 DAYS)"
 460  PRINT  TAB( 28);"A=OVERALL AVERAGE"
 465  PRINT 
 470  PRINT  TAB( 13);"DOWN                 CRITICAL                    UP"
 475  PRINT  TAB( 13);"*************************!*************************"
 480  REM  SET F(2) TO 29 FOR LEAP YRS
 485  REM 
 490 X = 0
 495  REM 
 500  REM  GENERATE THE BIO PLOT
 505 L = O + L
 510 C = 0
 515  FOR O = O TO L - 1
 520 C = C + 1
 525  FOR  X = 1  TO  51
 526 O$(X) = "-"
 527  NEXT  X
 528 O$(26) = "+"
 530 Y = 0
 535 X = ( SIN (K * (O / 23 -  INT (O / 23))) * 25) + 26
 540 O$(X) = "P"
 545 Y = Y + X
 550 X = ( SIN (K * (O / 33 -  INT (O / 33))) * 25) + 26
 555 O$(X) = "I"
 560 Y = Y + X
 565 X = ( SIN (K * (O / 28 -  INT (O / 28))) * 25) + 26
 570 O$(X) = "E"
 575 Y = (Y + X) / 3
 580 O$(Y) = "A"
 585  PRINT T$(P2); TAB( 3);M2;"/";D2;
 586  HTAB 13
 590  FOR  X = 1  TO  51
 592  PRINT  O$(X);
 594  NEXT  X
 596  PRINT 
 600  REM  INCREMENT DATE
 605  IF P2 = 8 THEN 620
 610 P2 = P2 + 1
 615  IF P2 > 7 THEN P2 = 1
 620 D2 = D2 + 1
 625  IF D2 > F(M2) THEN D2 = 1:M2 = M2 + 1
 630  IF M2 < 13 THEN 640
 635 M2 = 1:Y2 = Y2 + 1
 640 X = Y2:F(2) = 28
 645  GOSUB 1000
 650 F(2) = F(2) + X
 655  NEXT O
 660  PR# 0
 670  PRINT : PRINT "WANT INSTRUCTIONS?   (Y/N)";
 671  GET A$:Q$ = A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 671
 672  IF Q$ = "N" THEN 696
 673  PR# 1
 674  PRINT "THE BIORHYTHM THEORY....": POKE 1785,72
 675  PRINT : PRINT "POSTULATES THAT THERE ARE CERTAIN METABOLIC CYCLES,"
 676  PRINT "WHICH HAVE A CONSTANT PERIOD IN THE HUMAN BODY"
 677  PRINT "THERE ARE THREE (3) CYCLES..."
 678  PRINT "23 DAY PHYSICAL"
 679  PRINT "28 DAY EMOTIONAL"
 680  PRINT "33 DAY INTELLECTUAL"
 682  PRINT : PRINT "PHYSICAL IS ASSOCIATED WITH PHYSICAL VITALITY"
 684  PRINT "ENDURANCE AND ENERGY LEVEL."
 686  PRINT : PRINT "EMOTIONAL CORRESPONDS TO SENSITIVITY,"
 688  PRINT "INTUITION AND CHEERFULNESS."
 690  PRINT : PRINT "INTELLECTUAL IS RELATED TO MENTAL ALERTNESS,"
 692  PRINT "COGNITIVE POWER AND JUDGEMENT ABILITY."
 694  PRINT : PRINT "ALL THREE CYCLES START AT ZERO AT THE MOMENT OF BIRTH."
 695  PR# 0
 696  PR# 1: PRINT : PRINT : PRINT "": PRINT : PRINT "": PRINT : PRINT "": PRINT : PR# 0: GOTO 2000
 700  REM  FIND DAY OF WEEK
 705 N1 = M2 + 12 *  INT (.6 + 1 / M2)
 710 N2 = Y2 -  INT (.6 + 1 / M2)
 715 N3 =  INT (13 * (N1 + 1) / 5)
 720 N4 =  INT (5 * N2 / 4)
 725 N5 =  INT (N2 / 100)
 730 N6 =  INT (N2 / 400)
 735 N7 = N3 + N4 - N5 + N6 + D2 - 1
 740 P2 = N7 - 7 *  INT (N7 / 7) + 1
 745  RETURN 
 800  REM  FIND DAYS EXPENDED IN PRIOR MONTHS
 810 J2 = 0
 815  IF X = 1 THEN 850
 820  FOR I = 1 TO X - 1
 830 J2 = J2 + F(I)
 840  NEXT I
 850  RETURN 
 900  REM   GET NAME FILE
 910  GOTO 3000
 1000  REM   CHECK X FOR LEAP YEAR
 1005  IF X / 400 -  INT (X / 400) = 0 THEN 1020
 1010  IF X / 100 -  INT (X / 100) = 0 THEN 1030
 1015  IF X / 4 -  INT (X / 4) <  > 0 THEN 1030
 1020 X = 1
 1025  RETURN 
 1030 X = 0
 1035  RETURN 
 2000  REM   TURN AROUND INFORMATION
 2010  PRINT "ANOTHER BIORYTHM WITH SAME NAME?   (Y/N)";
 2011  GET Q$: IF Q$ <  > "Y" AND Q$ <  > "N" THEN 2011
 2020  PRINT : IF Q$ = "Y" THEN  GOTO 270
 2030  PRINT "ANOTHER BIORYTHM WITH NEW NAME?   (Y/N)";
 2031  GET E$: IF E$ <  > "Y" AND E$ <  > "N" THEN 2031
 2040  PRINT : IF E$ = "Y" THEN  GOTO 210
 2090  GOTO 9999
 5000  REM   INITIALIZE.
 5030  REM   SPECIAL END ROUTINE.
 5032  POKE  - 16368,0: CALL  - 936: RETURN 
 5034  POKE  - 16298,0: TEXT : GOTO 5032
 5036  GOSUB 5034
 5038  GOTO 9999
 5040  REM  PRINT CENTER ROUTINE.
 5042  HTAB 20 -  LEN (CT$) / 2
 5044  PRINT CT$
 5048  RETURN 
 60660  REM           

   -------- NOTICE --------

 ALL ENHANCEMENTS TO THIS PRO-
GRAM   ARE   COPYRIGHTED  1985
PUBLIC  DOMAIN RESEARCH.  ANY-
ONE WISHING TO DISTRIBUTE THIS
MODIFIED  PROGRAM   MUST  HAVE
 63999  REM THE EXPRESSED WRITTEN  CONSENT
OF THE  PUBLISHER.  LEGITIMATE
USER GROUPS  MAY  REQUEST THIS
PERMISSION.

    ----------//----------

