 10  REM  *****************
 20  REM  ***           ***
 30  REM  *** FLASH FAX ***
 40  REM  ***    V.3    ***
 50  REM  ***           ***
 60  REM  *****************
 70  REM 
 80  REM  *** JAY GRAVES
 90  REM  *** ORIGINAL STARTED 3/16/86 - FINISHED 'SUMMER' 86
 100  REM  *** ENHANCED 3/20/87
 110  REM  *** FREQUENT MISTAKES SUBROUTINE  - MAY 88
 120  REM 
 130  REM  *** MULTIPLICATION QUIZ FOR LEARNING ***
 140  REM  
 150  REM  *** VARIABLES ***
 160  REM 
 170  REM  *** PROGRAM VARIABLES
 180  REM  
 190  REM  *** R1 = 1ST # RANGE
 200  REM  *** R2 = 2ND # RANGE 
 210  REM  *** N1 = 1ST #  /  N1$ = STR$ (N1)
 220  REM  *** N2 = 2ND #  /  N2$ = STR$ (N2)
 221  REM  *** N2(K) = RND LOOP VARIABLE
 230  REM  *** N3 = CORRECT ANSWER  /  N3$ = STR$ (N3)
 240  REM  *** A = ANSWER GIVEN
 250  REM  *** T = TIME CHOICE
 260  REM  *** T1 = TIME VARIABLE
 270  REM  *** Q$ = DECISION VARIABLE
 280  REM  *** I = LOOP & COUNTER VARIABLE
 290  REM  *** J = LOOP VARIABLE
 295  REM  *** K = LOOP VARIABLE
 296  REM  *** L = LOOP VARIABLE
 300  REM  *** M = MENU CHOICE VARIABLE
 310  REM  *** WP = WRONG PROBLEM COUNTER
 320  REM  *** CP = CORRECT PROBLEM COUNTER
 330  REM  *** TP = TOTAL PROBLEM COUNTER
 340  REM  *** P = PERCENTAGE VARIABLE
 350  REM  *** X1 & X2 = CHECK VARIABLES - SAME PROBLEM TWICE
 360  REM  *** W1(X) = CURRENT # OF NUMERAL MISTAKES
 370  REM  
 380  REM  *** FILE VARIABLES
 390  REM  
 400  REM  *** F$ = FILE NAME
 410  REM  *** N$ = NAME VARIABLE
 420  REM  *** BS = BEST SCORE
 430  REM  *** W2(X) = DISK TOTAL # OF NUMERAL MISTAKES
 440  HOME 
 450  REM  *** COPY TO OWN DISK
 460  PRINT : PRINT : PRINT : HTAB 16: FLASH : PRINT "CAUTION": NORMAL 
 470  PRINT : PRINT : PRINT "THIS PROGRAM WRITES INFORMATION ONTO ": PRINT "THE DISK.  IT IS RECOMMENDED THAT YOU": PRINT "COPY IT TO IT'S OWN DISK."
 480  PRINT : PRINT : PRINT : HTAB 16: FLASH : PRINT "CAUTION": NORMAL 
 490  VTAB 23: PRINT "PRESS ANY KEY TO GO ON": GET A$
 500  REM 
 510  REM  *** SET UP VARIABLES
 520  CLEAR 
 521  DIM W1(12),W2(12),N2(12)
 530 R1 = 12:R2 = 10
 540  FOR J = 1 TO 12:W1(J) = 0: NEXT J
 550  REM 
 560  REM  *** GRAPHICS PAGE
 570  HOME : GR : COLOR= 15
 580  FOR I = 0 TO 39: VLIN 0,39 AT I: NEXT I
 590  REM  *** 'F' IN FLASH
 600  COLOR= 2
 610  HLIN 3,7 AT 3: HLIN 3,7 AT 4
 620  VLIN 5,14 AT 3: VLIN 5,14 AT 4: VLIN 8,9 AT 5: VLIN 8,9 AT 6
 630  REM  *** 'L' IN FLASH
 640  VLIN 3,14 AT 9: VLIN 3,14 AT 10
 650  HLIN 11,13 AT 13: HLIN 11,13 AT 14
 660  REM  *** 'A' IN FLASH
 670  VLIN 5,14 AT 15: VLIN 4,14 AT 16: VLIN 4,14 AT 19: VLIN 5,14 AT 20
 680  HLIN 17,18 AT 3: HLIN 17,18 AT 4: HLIN 17,18 AT 8: HLIN 17,18 AT 9
 690  REM  *** 'S' IN FLASH
 700  HLIN 22,26 AT 3: HLIN 22,26 AT 4: HLIN 22,26 AT 8: HLIN 22,26 AT 9: HLIN 22,26 AT 13: HLIN 22,26 AT 14
 710  VLIN 5,7 AT 22: VLIN 5,7 AT 23: VLIN 10,12 AT 25: VLIN 10,12 AT 26
 720  REM  *** 'H' IN FLASH
 730  VLIN 3,14 AT 28: VLIN 3,14 AT 29: VLIN 3,14 AT 32: VLIN 3,14 AT 33
 740  VLIN 8,9 AT 30: VLIN 8,9 AT 31
 750  REM  *** 'F' IN FAX
 760  HLIN 3,7 AT 20: HLIN 3,7 AT 21
 770  VLIN 22,31 AT 3: VLIN 22,31 AT 4: VLIN 25,26 AT 5: VLIN 25,26 AT 6
 780  REM  *** 'A' IN FAX
 790  VLIN 22,31 AT 9: VLIN 21,31 AT 10: VLIN 21,31 AT 13: VLIN 22,31 AT 14
 800  HLIN 11,12 AT 20: HLIN 11,12 AT 21: HLIN 11,12 AT 25: HLIN 11,12 AT 26
 810  REM  *** 'X' IN FAX
 820  VLIN 20,24 AT 16: VLIN 20,24 AT 17: VLIN 20,24 AT 20: VLIN 20,24 AT 21
 830  VLIN 24,27 AT 18: VLIN 24,27 AT 19
 840  VLIN 27,31 AT 16: VLIN 27,31 AT 17: VLIN 27,31 AT 20: VLIN 27,31 AT 21
 850  REM  *** PROBLEM
 860  COLOR= 0
 870  REM  *** '3'
 880  VLIN 16,22 AT 33: HLIN 30,33 AT 16: HLIN 30,33 AT 19: HLIN 30,33 AT 22
 890  REM  *** '6'
 900  VLIN 24,30 AT 30: VLIN 27,30 AT 33: HLIN 31,33 AT 24: HLIN 31,33 AT 27: HLIN 31,33 AT 30
 910  REM  *** 'x'
 920  PLOT 26,28: PLOT 27,29: PLOT 26,30: PLOT 28,28: PLOT 28,30
 930  REM  *** '===================='
 940  HLIN 26,35 AT 32
 950  VTAB 22: HTAB 15: PRINT "FLASH FAX"
 960  HTAB 8: PRINT "PROGRAMMED BY JAY GRAVES"
 970  FOR I = 1 TO 3000: NEXT I
 980  REM 
 990 D$ =  CHR$ (4)
 1000  REM  *** TITLE PAGE
 1010  TEXT : HOME 
 1020  VTAB 8: PRINT "(FIRST NAME ONLY)"
 1030  VTAB 5: INPUT "WHAT IS YOUR NAME? ";N$
 1040  REM  *** FILE NAME = "FILE " + NAME
 1050 F$ = "FILE " + N$
 1060  HOME : VTAB 5: PRINT "HAVE YOU EVER USED THIS PROGRAM BEFORE? ";: INPUT "";Q$
 1070  IF Q$ = "YES" OR Q$ = "Y" THEN 1190
 1080  REM  *** START FILE FOR NEW PERSON
 1100 BS = 0
 1110  FOR J = 1 TO 12:W2(J) = 0: NEXT J
 1120  PRINT 
 1130  PRINT D$;"OPEN "F$
 1140  PRINT D$;"WRITE "F$
 1150  PRINT BS
 1160  FOR J = 1 TO 12: PRINT W2(J): NEXT J
 1170  PRINT D$;"CLOSE "F$
 1180  PRINT : HOME 
 1181  GOTO 2600
 1190  FLASH : PRINT "HELLO, ";N$: NORMAL 
 1200  VTAB (5): PRINT "THIS PROGRAM WILL QUIZ YOU ON MULTIPLI-"
 1210  PRINT "CATION FACTS. "
 1220  VTAB (9): PRINT "YOU MUST TYPE IN THE CORRECT ANSWER BE-"
 1230  PRINT "FORE TIME RUNS OUT."
 1240  VTAB (12): PRINT "YOU CAN CHOSE THE AMOUNT OF TIME YOU"
 1250  PRINT "NEED."
 1260  VTAB (22): FLASH : PRINT "PRESS ANY KEY TO CONTINUE"
 1270  GET A$: NORMAL : HOME 
 1280  HOME 
 1290  VTAB (5): PRINT "AMOUNT OF TIME   1) 15 SECONDS"
 1300  HTAB (18): PRINT "2) 10 SECONDS"
 1310  HTAB (18): PRINT "3)  5 SECONDS"
 1320  VTAB (20): PRINT "======================================="
 1330  VTAB (22): PRINT "CHOSE THE TIME YOU NEED  ";
 1340  GET T
 1350  IF T >  = 1 OR T <  = 3 THEN 1370
 1360  GOTO 1280
 1370  ON T GOTO 1390,1440,1490
 1380  REM 
 1390  VTAB (5): HTAB (18): FLASH : PRINT "1) 15 SECONDS": NORMAL 
 1400  VTAB (22): PRINT "IS THIS THE CORRECT AMOUNT OF TIME?  ": PRINT "ENTER (Y/N) ": GET Q$
 1410  IF Q$ = "Y" THEN 1540
 1420  VTAB (22): PRINT "                                       "
 1430  GOTO 1280
 1440  VTAB (6): HTAB (18): FLASH : PRINT "2) 10 SECONDS": NORMAL 
 1450  VTAB (22): PRINT "IS THIS THE CORRECT AMOUNT OF TIME?  ": PRINT "ENTER (Y/N) ": GET Q$
 1460  IF Q$ = "Y" THEN 1540
 1470  VTAB (22): PRINT "                                       "
 1480  GOTO 1280
 1490  VTAB (7): HTAB (18): FLASH : PRINT "3)  5 SECONDS": NORMAL 
 1500  VTAB (22): PRINT "IS THIS THE CORRECT AMOUNT OF TIME?  ": PRINT "ENTER (Y/N) ": GET Q$
 1510  IF Q$ = "Y" THEN 1540
 1520  VTAB (22): PRINT "                                       "
 1530  GOTO 1280
 1540  HOME 
 1550  VTAB (10): PRINT "AS SOON AS THE PROBLEM APPEARS ON THE "
 1560  PRINT "ON THE SCREEN YOUR TIME BEGINS."
 1570  VTAB (15): PRINT "PRESS ANY KEY WHEN YOU ARE READY TO "
 1580  PRINT "BEGIN."
 1590  GET A$
 1600  IF T = 1 THEN T1 = 780
 1610  IF T = 2 THEN T1 = 520
 1620  IF T = 3 THEN T1 = 260
 1630 N1 = 1:N2 = 0
 1640  HOME 
 1650  IF TP = 20 THEN 2250
 1660 TP = TP + 1
 1670 X1 = N1:X2 = N2
 1680 N1 =  INT ( RND (1) * 12) + 1
 1690 N2 =  INT ( RND (1) * 10) + 1
 1700  IF X1 = N1 AND X2 = N2 THEN 1680
 1710 N3 = N1 * N2
 1720 N1$ =  STR$ (N1)
 1730 N2$ =  STR$ (N2)
 1740 N3$ =  STR$ (N3)
 1750  IF  LEN (N1$) = 1 THEN H1 = 21
 1760  IF  LEN (N1$) = 2 THEN H1 = 20
 1770  IF  LEN (N2$) = 1 THEN H2 = 21
 1780  IF  LEN (N2$) = 2 THEN H2 = 20
 1790  IF  LEN (N3$) = 1 THEN H3 = 21
 1800  IF  LEN (N3$) = 2 THEN H3 = 20
 1810  IF  LEN (N3$) = 3 THEN H3 = 19
 1820  VTAB (12): HTAB (H1): PRINT N1
 1830  VTAB (13): HTAB (18): PRINT "x"
 1840  VTAB (13): HTAB (H2): PRINT N2
 1850  VTAB (14): HTAB (18): PRINT "____"
 1860  VTAB 22: PRINT "Press <SPACE BAR> TO STOP TIMER"
 1870  FOR I = 1 TO T1
 1880  IF  PEEK ( - 16384) < 128 THEN 1910
 1890  POKE  - 16368,0
 1900  GOTO 1930
 1910  NEXT I
 1920  GOTO 2140
 1930  VTAB 22: PRINT "ENTER YOUR ANSWER                         "
 1940  VTAB (16): HTAB (H3): INPUT "";A
 1950  IF A = N3 THEN 2060
 1960  REM  *** WRONG ANSWER
 1970 WP = WP + 1
 1980  VTAB 22: PRINT "I'M SORRY.  YOUR ANSWER WAS INCORRECT"
 1990  PRINT "THE CORRECT ANSWER WAS "N3"."
 2000 W1(N1) = W1(N1) - 1
 2010  IF N1 = N2 THEN 2030
 2020 W1(N2) = W1(N2) - 1
 2030  FLASH : VTAB 16: HTAB (H3): PRINT N3: NORMAL 
 2040  FOR I = 1 TO 2000: NEXT I
 2050  GOTO 1640
 2060  REM  *** CORRECT ANSWER
 2070 CP = CP + 1
 2080  VTAB 22: FLASH : PRINT "THAT'S CORRECT!!! ": NORMAL 
 2090 W1(N1) = W1(N1) + 1
 2100  IF N1 = N2 THEN 2120
 2110 W1(N2) = W1(N2) + 1
 2120  FOR I = 1 TO 2000: NEXT I
 2130  GOTO 1640
 2140  REM  *** TIME RUNS OUT  
 2150 WP = WP + 1
 2160  VTAB 22: PRINT "I'M SORRY.  YOUR TIME HAS RUN OUT.    "
 2170  PRINT "THE CORRECT ANSWER WAS "N3"."
 2180 W1(N1) = W1(N1) - 1
 2190  IF N1 = N2 THEN 2210
 2200 W1(N2) = W1(N2) - 1
 2210  FLASH : VTAB 16: HTAB (H3): PRINT N3: NORMAL 
 2220  FOR I = 1 TO 2000: NEXT I
 2230  GOTO 1640
 2240  REM 
 2250  REM  *** GET OLD FILE AND COMPARE DATA
 2260  PRINT 
 2270 D$ =  CHR$ (4)
 2280  PRINT D$;"OPEN "F$
 2290  PRINT D$;"READ "F$
 2300  INPUT BS
 2310  FOR J = 1 TO 12: INPUT W2(J): NEXT J
 2320  PRINT D$;"CLOSE "F$
 2330  GOSUB 2600
 2340 P = CP * 5
 2350  IF P > BS THEN  GOTO 2420
 2360  HOME : VTAB 5: PRINT "YOUR SCORE WAS ";P;"."
 2370  VTAB 10: PRINT "YOU DID NOT BEAT YOUR BEST SCORE "
 2380  PRINT "OF ";BS;"."
 2390  FOR I = 1 TO 2000: NEXT I
 2400  HOME 
 2410  GOTO 2530
 2420  HOME : VTAB 5: FLASH : PRINT "CONGRATULATIONS !!": NORMAL 
 2430  VTAB 7: PRINT "YOU BEAT YOUR HIGH SCORE."
 2440  VTAB 9: PRINT "YOUR NEW HIGH SCORE IS ";P:BS = P
 2450  FOR I = 1 TO 2000: NEXT I
 2460  FOR J = 1 TO 12:W2(J) = W2(J) + W1(J): NEXT J
 2470  PRINT D$;"OPEN "F$
 2480  PRINT D$;"WRITE "F$
 2490  PRINT BS
 2500  FOR J = 1 TO 12: PRINT W2(J): NEXT J
 2510  PRINT D$;"CLOSE "F$
 2520  PRINT : HOME : VTAB 1: PRINT "YOUR NEW HIGH SCORE IS SAVED ON FILE."
 2530  VTAB 5: INPUT "WOULD YOU LIKE TO PLAY AGAIN? (Y/N) ";Q$
 2540  IF Q$ = "Y" THEN  CLEAR : GOTO 1280
 2550  HOME : VTAB 5: INPUT "DOES SOMEONE ELSE WANT TO PLAY? ";Q$
 2560  IF Q$ = "Y" THEN 510
 2570  HOME : VTAB 5: PRINT "THANKS FOR PLAYING ";N$"."
 2580  VTAB 8: PRINT "COME BACK AND PLAY AGAIN !"
 2590  GOTO 3340
 2600  REM  *** FIRST TIME TEST TO SEED DATA FILE
 2610  REM  >>>>>>> EXPLAIN SET UP PROBLEMS
 2620 T1 = 780
 2630  FOR N1 = 1 TO R1
 2631  GOSUB 4000
 2640  FOR K = 1 TO R2
 2650 X1 = N1:X2 = N2
 2660  IF X1 = N1 AND X2 = N2 THEN 1680
 2670 N3 = N1 * N2(K)
 2680 N1$ =  STR$ (N1)
 2690 N2$ =  STR$ (N2(K))
 2700 N3$ =  STR$ (N3)
 2710  IF  LEN (N1$) = 1 THEN H1 = 21
 2720  IF  LEN (N1$) = 2 THEN H1 = 20
 2730  IF  LEN (N2$) = 1 THEN H2 = 21
 2740  IF  LEN (N2$) = 2 THEN H2 = 20
 2750  IF  LEN (N3$) = 1 THEN H3 = 21
 2760  IF  LEN (N3$) = 2 THEN H3 = 20
 2770  IF  LEN (N3$) = 3 THEN H3 = 19
 2780  VTAB (12): HTAB (H1): PRINT N1
 2790  VTAB (13): HTAB (18): PRINT "x"
 2800  VTAB (13): HTAB (H2): PRINT N2(K)
 2810  VTAB (14): HTAB (18): PRINT "____"
 2820  VTAB 22: PRINT "Press <SPACE BAR> TO STOP TIMER"
 2830  FOR I = 1 TO T1
 2840  IF  PEEK ( - 16384) < 128 THEN 2870
 2850  POKE  - 16368,0
 2860  GOTO 2890
 2870  NEXT I
 2880  GOTO 3120
 2890  VTAB 22: PRINT "ENTER YOUR ANSWER                         "
 2900  VTAB (16): HTAB (H3): INPUT "";A
 2910  IF A = N3 THEN 3030
 2920  REM  *** WRONG ANSWER
 2940  VTAB 22: PRINT "I'M SORRY.  YOUR ANSWER WAS INCORRECT"
 2950  PRINT "THE CORRECT ANSWER WAS "N3"."
 2960 W2(N1) = W2(N1) - 1
 2970  IF N1 = N2 THEN 2990
 2980 W2(N2(K)) = W2(N2(K)) + 1
 2990  FLASH : VTAB 16: HTAB (H3): PRINT N3: NORMAL 
 3000  FOR I = 1 TO 2000: NEXT I
 3010  NEXT K: NEXT N1
 3020  GOTO 3240
 3030  REM  *** CORRECT ANSWER
 3050  VTAB 22: FLASH : PRINT "THAT'S CORRECT!!! ": NORMAL 
 3060 W2(N1) = W2(N1) + 1
 3070  IF N1 = N2(K) THEN 3090
 3080 W2(N2(K)) = W2(N2(K)) + 1
 3090  FOR I = 1 TO 2000: NEXT I
 3100  NEXT K: NEXT N1
 3110  GOTO 3240
 3120  REM  *** TIME RUNS OUT  
 3140  VTAB 22: PRINT "I'M SORRY.  YOUR TIME HAS RUN OUT.    "
 3150  PRINT "THE CORRECT ANSWER WAS "N3"."
 3160 W2(N1) = W2(N1) - 1
 3170  IF N1 = N2(K) THEN 3190
 3180 W2(N2(K)) = W2(N2(K)) + 1
 3190  FLASH : VTAB 16: HTAB (H3): PRINT N3: NORMAL 
 3200  FOR I = 1 TO 2000: NEXT I
 3210  NEXT K: NEXT N1
 3220  REM  *** SAVE FIRST TIME RESULTS TO DISK
 3230 BS = 0
 3240  PRINT D$;"OPEN "F$
 3250  PRINT D$;"WRITE "F$
 3260  PRINT BS: FOR J = 1 TO 12: PRINT W2(J): NEXT J
 3270  PRINT D$;"CLOSE "F$
 3280  HOME : PRINT "YOU ARE NOW DONE WITH THE TEST.  PLEASE": PRINT "CHOSE ONE OF THE FOLLOWING.": PRINT : PRINT 
 3290  PRINT "     1)  LET SOMEONE ELSE TRY": PRINT 
 3300  PRINT "     2)  TAKE A QUIZ": PRINT 
 3310  PRINT "     3)  QUIT": PRINT : PRINT : PRINT 
 3320  PRINT : PRINT : PRINT : PRINT : INPUT "ENTER CHOICE: ";M
 3330  HOME : ON M GOTO 510,1180,3340
 3340  REM  *** END SUBROUTINE
 3350  HOME : PRINT : PRINT : PRINT : PRINT : PRINT "PLEASE REMOVE THE DISK AND TURN THE ": PRINT "COMPUTER OFF.": PRINT : PRINT "THANKS FOR PLAYING"
 3360  GOTO 3360: REM  *** ENDLESS HOLD LOOP
 4000  REM  *** RANDOMIZE DATA SEED VARIABLES
 4005  FOR K = 1 TO R2:N2(K) = K: NEXT K:K = 0
 4010  FOR K = 1 TO R2
 4020 N2 =  INT ( RND (1) * R2) + 1
 4030  IF K = 1 THEN 4070
 4040  FOR L = 1 TO K - 1
 4050  IF N2 = N2(K) THEN 4020
 4060  NEXT L
 4070 N2(K) = N2
 4080  NEXT K
 4090  RETURN 
