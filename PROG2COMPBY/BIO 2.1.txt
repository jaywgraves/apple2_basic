 100  REM : BIORHYTHM TWO
 120 L = 0:Z = .99999:T = 14:P = 3.14159265
 130  TEXT : HOME : PRINT  TAB( 8);"BIORHYTHM"
 140  GOSUB 1200: PRINT : PRINT 
 150  PRINT "ENTER BIRTH DATE"
 160  GOSUB 500
 170  GOSUB 600
 180 JB = JD
 190  PRINT : PRINT "ENTER START DATE FOR CHART"
 200  GOSUB 500
 210  GOSUB 600
 220 JC = JD
 230  IF JC >  = JB THEN 270
 240  PRINT "CHART DATE CAN'T BE EARLIER"
 250  PRINT "THAN BIRTH DATE.  TRY AGAIN."
 260  GOTO 140
 270  PRINT : PRINT : INPUT "HOW MANY DAYS: ";DD: HOME : FOR J = 1 TO 1000: NEXT 
 280  GOSUB 700
 300 N = JC - JB
 310 V = 23: GOSUB 800: GOSUB 850
 320 V = 28: GOSUB 800: GOSUB 850
 330 V = 33: GOSUB 800: GOSUB 850
 340  GOSUB 1000
 350  PRINT  TAB( 1);C$; TAB( 9);L$
 360 JC = JC + 1:L = L + 1: IF L < DD THEN 300
 370 R$ =  CHR$ (27)
 380  GET R$: IF  ASC (R$) = 27 THEN  END 
 390  IF R$ = "E" THEN 120
 400 L = 0: GOTO 280
 500  PRINT 
 505  INPUT "MONTH (1 TO 12)? ";M
 510 M =  INT (M): IF M < 1 OR M > 12 THEN 505
 520  INPUT "DAY (1 TO 31)? ";D
 530 D =  INT (D): IF D < 1 OR D > 31 THEN 520
 540  INPUT "YEAR? ";Y
 550 Y =  INT (Y): IF Y = 0 THEN 540
 560  IF Y > 99 THEN 580
 570 Y = Y + 1900: PRINT Y;" ASSUMED."
 580  RETURN 
 600 W =  INT ((M - 14) / 12 + Z)
 610 JD =  INT (1461 * (Y + 4800 + W) / 4)
 620 B = 367 * (M - 2 - W * 12) / 12
 630  IF B < 0 THEN B = B + Z
 640 B =  INT (B):JD = JD + B
 650 B =  INT ( INT (3 * (Y + 4900 + W) / 100) / 4)
 660 JD = JD + D - 32075 - B
 670  RETURN 
 700  HOME 
 701  PR# 1
 710  PRINT  TAB( 15);"BIORHYTHM"
 720  PRINT : PRINT  TAB( 1);"--DATE--"; TAB( 12);
 730  PRINT "DOWN              0                  UP"
 740  PRINT  TAB( 9);U$
 750  RETURN 
 800 W =  INT (N / V):R = N - W * V
 810  RETURN 
 850  IF V <  > 23 THEN 900
 855 L$ =  CHR$ (32): FOR J = 1 TO 4
 860 L$ = L$ + L$: NEXT 
 870 L$ = L$ +  LEFT$ (L$,13)
 880 L$ =  LEFT$ (L$,T) +  CHR$ (33) +  RIGHT$ (L$,T)
 890  IF V = 23 THEN C$ = "P"
 900  IF V = 28 THEN C$ = "E"
 910  IF V = 33 THEN C$ = "I"
 920 W = R / V:W = W * 2 * P
 930 W = T *  SIN (W):W = W + T + 1.5
 940 W =  INT (W):A$ =  MID$ (L$,W,1)
 950  IF A$ = "P" OR A$ = "E" OR A$ = "*" THEN C$ = "*"
 955  IF W = 1 THEN 980
 957  IF W = T + T + 1 THEN 990
 960 L$ =  LEFT$ (L$,W - 1) + C$ +  RIGHT$ (L$,T + T + 1 - W)
 970  RETURN 
 980 L$ = C$ +  RIGHT$ (L$,T + T): RETURN 
 990 L$ =  LEFT$ (L$,T + T) + C$: RETURN 
 1000 W = JC + 68569:R =  INT (4 * W / 146097)
 1010 W = W -  INT ((146097 * R + 3) / 4)
 1020 Y =  INT (4000 * (W + 1) / 1461001)
 1030 W = W -  INT (1461 * Y / 4) + 31
 1040 M =  INT (80 * W / 2447)
 1050 D = W -  INT (2447 * M / 80)
 1060 W =  INT (M / 11):M = M + 2 - 12 * W
 1070 Y = 100 * (R - 49) + Y + W
 1080 A$ =  STR$ (M)
 1090 C$ = A$ + "/"
 1100 A$ =  STR$ (D)
 1110 C$ = C$ + A$ + "/"
 1120 A$ =  STR$ (Y):W =  LEN (A$)
 1130 C$ = C$ +  MID$ (A$,W - 1,2)
 1140  RETURN 
 1200 U$ = "-": FOR J = 1 TO 4
 1210 U$ = U$ + U$: NEXT 
 1220 U$ = U$ +  LEFT$ (U$,13)
 1230  RETURN 
