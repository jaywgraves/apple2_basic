 10  REM  *** RA-SAVE EMPLOY-20
 20  REM  *** JAY GRAVES
 30  REM  *** 11-18-86
 40  HOME :D$ =  CHR$ (4)
 50  DIM N(20),X(20),A(20),Y(20),S(20)
 60  REM  *** VARIABLE NAMES
 70  REM  * N = ID#
 80  REM  * X = SEX
 90  REM  * A = AGE
 100  REM  * Y = YRS OF SERVICE
 110  REM  * S = SALARY
 120  REM  *** TITLE PG
 130  VTAB 5: HTAB 5: PRINT "This program will create a file"
 140  PRINT "named EMPLOY-20. The file will have"
 150  PRINT "the following information: ID#, SEX,"
 160  PRINT "age, yrs. of service, and salary."
 170  VTAB 22: PRINT "PRESS ANY KEY TO CONTINUE": GET A$: HOME 
 180  REM  *** LOAD DATA
 190  FOR I = 1 TO 11
 200  READ N(I)
 210  READ X(I)
 220  READ A(I)
 230  READ Y(I)
 240  READ S(I)
 250  NEXT I
 260  REM  *** SHOW TABLE
 270  HTAB 22: PRINT "Yrs. of"
 280  HTAB 2: PRINT "ID#";: HTAB 9: PRINT "Sex";: HTAB 16: PRINT "Age";: HTAB 22: PRINT "Service";: HTAB 33: PRINT "Salary"
 290  PRINT "======================================="
 300  REM  *** PRINT TABLE
 310  PRINT 
 320  FOR I = 1 TO 11
 330  PRINT N(I);: HTAB 10: PRINT X(I);: HTAB 17: PRINT A(I);: HTAB 25: PRINT Y(I);: HTAB 33: PRINT "$";S(I)
 340  NEXT I
 350  VTAB 22: PRINT "PRESS ANY KEY TO SAVE THE FILE"
 360  GET A$: HOME 
 370  REM  *** DATA
 380  DATA  2735,1,47,13,20200
 390  DATA  2980,2,33,6,14300
 400  DATA  3865,2,41,15,23900
 410  DATA  4222,1,22,2,11400
 420  DATA  4740,1,59,7,19200
 430  DATA  5200,2,25,3,13000
 440  DATA  5803,1,33,13,21500
 450  DATA  7242,2,28,4,13400
 460  DATA  7341,1,68,30,25500
 470  DATA  8004,1,35,6,14300
 480  DATA  9327,2,21,3,9200
 490  REM   *** SAVE FILE EMPLOY-20
 500  VTAB 12: PRINT "Saving the file: EMPLOY-20"
 510  PRINT D$;"OPEN EMPLOY-20,L20"
 520  FOR I = 1 TO 11
 530  PRINT D$;"WRITE EMPLOY-20,R"I
 540  PRINT N(I)
 550  PRINT X(I)
 560  PRINT A(I)
 570  PRINT Y(I)
 580  PRINT S(I)
 590  NEXT I
 600  PRINT D$;"CLOSE EMPLOY-20
 610  VTAB 18: PRINT "The file has been saved."
 620  VTAB 22: PRINT "PRESS ANY KEY TO END"
 630  GET A$: HOME : END 
