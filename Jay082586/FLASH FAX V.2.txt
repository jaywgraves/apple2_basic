 10  REM  *****************
 20  REM  ***           ***
 30  REM  *** FLASH FAX ***
 40  REM  ***           ***
 50  REM  *****************
 60  REM 
 70  REM  *** JAY GRAVES
 80  REM  *** ORIGINAL STARTED 3/16/86 - FINISHED 'SUMMER' 86
 90  REM  *** ENHANCED 3/20/87
 100  REM 
 110  REM  *** MULTIPLICATION QUIZ FOR LEARNING ***
 120  REM  
 130  REM  *** VARIABLES ***
 140  REM 
 150  REM  *** PROGRAM VARIABLES
 160  REM  
 170  REM  *** N1 = 1ST #  /  N1$ = STR$ (N1)
 180  REM  *** N2 = 2ND #  /  N2$ = STR$ (N2)
 190  REM  *** N3 = CORRECT ANSWER  /  N3$ = STR$ (N3)
 200  REM  *** A = ANSWER GIVEN
 210  REM  *** T = TIME CHOICE
 220  REM  *** T1 = TIME VARIABLE
 230  REM  *** Q$ = DECISION VARIABLE
 240  REM  *** I = LOOP & COUNTER VARIABLE
 250  REM  *** WP = WRONG PROBLEM COUNTER
 260  REM  *** CP = CORRECT PROBLEM COUNTER
 270  REM  *** TP = TOTAL PROBLEM COUNTER
 280  REM  *** P = PERCENTAGE VARIABLE
 290  REM  *** X1 & X2 = CHECK VARIABLES - SAME PROBLEM TWICE
 300  REM  
 310  REM  *** FILE VARIABLES
 320  REM  
 330  REM  *** F$ = FILE NAME
 340  REM  *** N$ = NAME VARIABLE
 350  REM  *** BS = BEST SCORE
 360  REM  
 370  REM  *** GRAPHICS PAGE
 375  CLEAR 
 380  REM 
 400  HOME : GR : COLOR= 15
 410  FOR I = 0 TO 39: VLIN 0,39 AT I: NEXT I
 420  REM  *** 'F' IN FLASH
 430  COLOR= 2
 440  HLIN 3,7 AT 3: HLIN 3,7 AT 4
 450  VLIN 5,14 AT 3: VLIN 5,14 AT 4: VLIN 8,9 AT 5: VLIN 8,9 AT 6
 460  REM  *** 'L' IN FLASH
 470  VLIN 3,14 AT 9: VLIN 3,14 AT 10
 480  HLIN 11,13 AT 13: HLIN 11,13 AT 14
 490  REM  *** 'A' IN FLASH
 500  VLIN 5,14 AT 15: VLIN 4,14 AT 16: VLIN 4,14 AT 19: VLIN 5,14 AT 20
 510  HLIN 17,18 AT 3: HLIN 17,18 AT 4: HLIN 17,18 AT 8: HLIN 17,18 AT 9
 520  REM  *** 'S' IN FLASH
 530  HLIN 22,26 AT 3: HLIN 22,26 AT 4: HLIN 22,26 AT 8: HLIN 22,26 AT 9: HLIN 22,26 AT 13: HLIN 22,26 AT 14
 540  VLIN 5,7 AT 22: VLIN 5,7 AT 23: VLIN 10,12 AT 25: VLIN 10,12 AT 26
 550  REM  *** 'H' IN FLASH
 560  VLIN 3,14 AT 28: VLIN 3,14 AT 29: VLIN 3,14 AT 32: VLIN 3,14 AT 33
 570  VLIN 8,9 AT 30: VLIN 8,9 AT 31
 580  REM  *** 'F' IN FAX
 590  HLIN 3,7 AT 20: HLIN 3,7 AT 21
 600  VLIN 22,31 AT 3: VLIN 22,31 AT 4: VLIN 25,26 AT 5: VLIN 25,26 AT 6
 610  REM  *** 'A' IN FAX
 620  VLIN 22,31 AT 9: VLIN 21,31 AT 10: VLIN 21,31 AT 13: VLIN 22,31 AT 14
 630  HLIN 11,12 AT 20: HLIN 11,12 AT 21: HLIN 11,12 AT 25: HLIN 11,12 AT 26
 640  REM  *** 'X' IN FAX
 650  VLIN 20,24 AT 16: VLIN 20,24 AT 17: VLIN 20,24 AT 20: VLIN 20,24 AT 21
 660  VLIN 24,27 AT 18: VLIN 24,27 AT 19
 670  VLIN 27,31 AT 16: VLIN 27,31 AT 17: VLIN 27,31 AT 20: VLIN 27,31 AT 21
 680  REM  *** PROBLEM
 690  COLOR= 0
 700  REM  *** '3'
 710  VLIN 16,22 AT 33: HLIN 30,33 AT 16: HLIN 30,33 AT 19: HLIN 30,33 AT 22
 720  REM  *** '6'
 730  VLIN 24,30 AT 30: VLIN 27,30 AT 33: HLIN 31,33 AT 24: HLIN 31,33 AT 27: HLIN 31,33 AT 30
 740  REM  *** 'x'
 750  PLOT 26,28: PLOT 27,29: PLOT 26,30: PLOT 28,28: PLOT 28,30
 760  REM  *** '===================='
 770  HLIN 26,35 AT 32
 780  VTAB 22: HTAB 15: PRINT "FLASH FAX"
 790  HTAB 8: PRINT "PROGRAMMED BY JAY GRAVES"
 800  FOR I = 1 TO 3000: NEXT I
 810  REM 
 820 D$ =  CHR$ (4)
 840  REM  *** TITLE PAGE
 850  TEXT : HOME 
 860  VTAB 8: PRINT "(FIRST NAME ONLY)"
 870  VTAB 5: INPUT "WHAT IS YOUR NAME? ";N$
 880  REM  *** FILE NAME = "FILE " + NAME
 890 F$ = "FILE " + N$
 900  HOME : VTAB 5: PRINT "HAVE YOU EVER USED THIS PROGRAM BEFORE? ";: INPUT "";Q$
 910  IF Q$ = "NO" OR Q$ = "N" THEN 1000
 920  REM  *** START FILE FOR NEW PERSON
 930 BS = 0
 940  PRINT 
 950  PRINT D$;"OPEN "F$
 960  PRINT D$;"WRITE "F$
 970  PRINT BS
 980  PRINT D$;"CLOSE "F$
 990  PRINT : HOME 
 1000  FLASH : PRINT "HELLO, ";N$: NORMAL 
 1010  VTAB (5): PRINT "This program will quiz you on multipli-"
 1020  PRINT "cation facts.  "
 1030  VTAB (9): PRINT "You must type in the correct answer be-"
 1040  PRINT "fore time runs out."
 1050  VTAB (12): PRINT "You can chose the amount of time you"
 1060  PRINT "need."
 1070  VTAB (22): FLASH : PRINT "PRESS ANY KEY TO CONTINUE"
 1080  GET A$: NORMAL : HOME 
 1090  HOME 
 1100  VTAB (5): PRINT "AMOUNT OF TIME   1) 15 SECONDS"
 1110  HTAB (18): PRINT "2) 10 SECONDS"
 1120  HTAB (18): PRINT "3)  5 SECONDS"
 1130  VTAB (20): PRINT "======================================="
 1140  VTAB (22): PRINT "CHOSE THE TIME YOU NEED  ";
 1150  GET T
 1160  IF T >  = 1 OR T <  = 3 THEN 1180
 1170  GOTO 1090
 1180  ON T GOTO 1200,1250,1300
 1190  REM 
 1200  VTAB (5): HTAB (18): FLASH : PRINT "1) 15 SECONDS": NORMAL 
 1210  VTAB (22): PRINT "Is this the correct amount of time?  ": PRINT "ENTER (Y/N) ": GET Q$
 1220  IF Q$ = "Y" THEN 1350
 1230  VTAB (22): PRINT "                                       "
 1240  GOTO 1090
 1250  VTAB (6): HTAB (18): FLASH : PRINT "2) 10 SECONDS": NORMAL 
 1260  VTAB (22): PRINT "Is this the correct amount of time?  ": PRINT "ENTER (Y/N) ": GET Q$
 1270  IF Q$ = "Y" THEN 1350
 1280  VTAB (22): PRINT "                                       "
 1290  GOTO 1090
 1300  VTAB (7): HTAB (18): FLASH : PRINT "3)  5 SECONDS": NORMAL 
 1310  VTAB (22): PRINT "Is this the correct amount of time?  ": PRINT "ENTER (Y/N) ": GET Q$
 1320  IF Q$ = "Y" THEN 1350
 1330  VTAB (22): PRINT "                                       "
 1340  GOTO 1090
 1350  HOME 
 1360  VTAB (10): PRINT "As soon as the problem appears on the "
 1370  PRINT "on the screen your time begins."
 1380  VTAB (15): PRINT "Press any key when you are ready to "
 1390  PRINT "begin."
 1400  GET A$
 1410  IF T = 1 THEN T1 = 780
 1420  IF T = 2 THEN T1 = 520
 1430  IF T = 3 THEN T1 = 260
 1440 N1 = 1:N2 = 0
 1450  HOME 
 1460  IF TP = 20 THEN 1980
 1470 TP = TP + 1
 1480 X1 = N1:X2 = N2
 1490 N1 =  INT ( RND (1) * 12) + 1
 1500 N2 =  INT ( RND (1) * 10) + 1
 1510  IF X1 = N1 AND X2 = N2 THEN 1490
 1520 N3 = N1 * N2
 1530 N1$ =  STR$ (N1)
 1540 N2$ =  STR$ (N2)
 1550 N3$ =  STR$ (N3)
 1560  IF  LEN (N1$) = 1 THEN H1 = 21
 1570  IF  LEN (N1$) = 2 THEN H1 = 20
 1580  IF  LEN (N2$) = 1 THEN H2 = 21
 1590  IF  LEN (N2$) = 2 THEN H2 = 20
 1600  IF  LEN (N3$) = 1 THEN H3 = 21
 1610  IF  LEN (N3$) = 2 THEN H3 = 20
 1620  IF  LEN (N3$) = 3 THEN H3 = 19
 1630  VTAB (12): HTAB (H1): PRINT N1
 1640  VTAB (13): HTAB (18): PRINT "x"
 1650  VTAB (13): HTAB (H2): PRINT N2
 1660  VTAB (14): HTAB (18): PRINT "____"
 1670  VTAB 22: PRINT "Press <SPACE BAR> TO STOP TIMER"
 1680  FOR I = 1 TO T1
 1690  IF  PEEK ( - 16384) < 128 THEN 1720
 1700  POKE  - 16368,0
 1710  GOTO 1740
 1720  NEXT I
 1730  GOTO 1900
 1740  VTAB 22: PRINT "ENTER YOUR ANSWER                         "
 1750  VTAB (16): HTAB (H3): INPUT "";A
 1760  IF A = N3 THEN 1840
 1770  REM  *** WRONG ANSWER
 1780 WP = WP + 1
 1790  VTAB 22: PRINT "I'M SORRY.  YOUR ANSWER WAS INCORRECT"
 1800  PRINT "THE CORRECT ANSWER WAS "N3"."
 1810  FLASH : VTAB 16: HTAB (H3): PRINT N3: NORMAL 
 1820  FOR I = 1 TO 2000: NEXT I
 1830  GOTO 1450
 1840  REM  *** CORRECT ANSWER
 1850 CP = CP + 1
 1870  VTAB 22: FLASH : PRINT "THAT'S CORRECT!!! ": NORMAL 
 1880  FOR I = 1 TO 2000: NEXT I
 1890  GOTO 1450
 1900  REM  *** TIME RUNS OUT  
 1910 WP = WP + 1
 1920  VTAB 22: PRINT "I'M SORRY.  YOUR TIME HAS RUN OUT.    "
 1930  PRINT "THE CORRECT ANSWER WAS "N3"."
 1940  FLASH : VTAB 16: HTAB (H3): PRINT N3: NORMAL 
 1950  FOR I = 1 TO 2000: NEXT I
 1960  GOTO 1450
 1970  REM 
 1980  REM  *** GET OLD FILE AND COMPARE DATA
 1990  PRINT 
 1995 D$ =  CHR$ (4)
 2000  PRINT D$;"OPEN "F$
 2010  PRINT D$;"READ "F$
 2020  INPUT BS
 2030  PRINT D$;"CLOSE "F$
 2040 P = CP * 5
 2050  IF P > BS THEN  GOTO 2120
 2060  HOME : VTAB 5: PRINT "YOUR SCORE WAS ";P;"."
 2070  VTAB 10: PRINT "YOU DID NOT BEAT YOUR BEST SCORE "
 2080  PRINT "OF ";BS;"."
 2090  FOR I = 1 TO 2000: NEXT I
 2100  HOME 
 2110  GOTO 2210
 2120  HOME : VTAB 5: FLASH : PRINT "CONGRATULATIONS !!": NORMAL 
 2130  VTAB 7: PRINT "YOU BEAT YOUR HIGH SCORE."
 2140  VTAB 9: PRINT "YOUR NEW HIGH SCORE IS ";P:BS = P
 2150  FOR I = 1 TO 2000: NEXT I
 2160  PRINT D$;"OPEN "F$
 2170  PRINT D$;"WRITE "F$
 2180  PRINT BS
 2190  PRINT D$;"CLOSE "F$
 2200  PRINT : HOME : VTAB 1: PRINT "YOUR NEW HIGH SCORE IS SAVED ON FILE."
 2210  VTAB 5: INPUT "WOULD YOU LIKE TO PLAY AGAIN? (Y/N) ";Q$
 2220  IF Q$ = "Y" THEN  CLEAR : GOTO 1090
 2230  HOME : VTAB 5: INPUT "DOES SOMEONE ELSE WANT TO PLAY? ";Q$
 2240  IF Q$ = "Y" THEN 370
 2250  HOME : VTAB 5: PRINT "THANKS FOR PLAYING ";N$"."
 2260  VTAB 8: PRINT "COME BACK AND PLAY AGAIN !"
 2270  END 
