 10  REM  *** SF-SAVE POINTER
 20  REM  *** JAY GRAVES
 30  REM  *** 11-19-86
 40  HOME :D$ =  CHR$ (4)
 50  DIM N(20),C(20)
 60  REM  * N = ID #
 70  REM  * C = RECORD #
 80  VTAB 5: PRINT "THIS PROGRAM WILL CREATE A POINTER FILE"
 81  PRINT "CONTAINING ID #'S AND RECORD #'S."
 90  VTAB 22: PRINT "PRESS ANY KEY TO CONTINUE": GET A$
 100  REM  *** LOAD DATA
 110  HOME 
 120  FOR I = 1 TO 11
 130  READ N(I)
 140  READ C(I)
 150  NEXT I
 160  REM  *** DATA
 170  DATA  2735,1,2980,2,3865,3,4222,4,4740,5,5200,6,5803,7,7242,8,7341,9,8004,10,9327,11
 180  REM  *** SHOW TABLE
 190  HOME 
 200  PRINT "ID #";: HTAB 10: PRINT "RECORD #"
 201  PRINT "_________________"
 202  PRINT 
 210  FOR I = 1 TO 11
 220  PRINT N(I);: HTAB 13: PRINT C(I)
 230  NEXT I
 240  VTAB 22: PRINT "PRESS ANY KEY TO SAVE FILE": GET A$
 250  HOME 
 260  VTAB 10: PRINT "SAVING POINTER FILE "
 270  PRINT D$;"OPEN POINTER"
 280  PRINT D$;"WRITE POINTER"
 290  FOR I = 1 TO 11
 300  PRINT N(I)
 310  PRINT C(I)
 320  NEXT I
 330  PRINT D$;"CLOSE POINTER"
 340  VTAB 10: PRINT "POINTER FILE IS SAVED"
 350  VTAB 22: END 
