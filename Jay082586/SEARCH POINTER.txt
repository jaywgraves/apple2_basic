 10  REM  *** SEARCH POINTER
 20  REM  *** JAY GRAVES
 30  REM  *** 11-21-86
 40  HOME :D$ =  CHR$ (4)
 50  DIM N(20),X(20),A(20),Y(20),S(20),C(20)
 60  REM  *** VARIABLES
 70  REM  * N = ID#
 80  REM  * X = SEX
 90  REM  * A = AGE
 100  REM  * Y = YRS. OF SERVICE
 110  REM  * S = SALARY
 120  REM  * C = RECORD #
 130  REM  *** TITLE PG
 140  VTAB 5: PRINT "THIS PROGRAM WILL SEARCH A SEQUENTIAL"
 150  PRINT "POINTER FILE AND PRINT THE INFORMATION"
 160  PRINT "FROM A RANDOM ACCESS FILE."
 170  VTAB 22: PRINT "PRESS ANY KEY TO CONTINUE": GET A$: HOME 
 171  PRINT 
 180  REM  *** GET POINTER FILE
 190  PRINT D$;"OPEN POINTER"
 200  PRINT D$;"READ POINTER"
 205  FOR I = 1 TO 11
 210  INPUT N(I)
 220  INPUT C(I)
 230  NEXT I
 240  PRINT D$;"CLOSE POINTER"
 250  REM  *** PRINT POINTER
 260  HOME 
 270  PRINT "ID #";: HTAB 10: PRINT "RECORD #"
 272  PRINT "-----------------"
 280  FOR I = 1 TO 11
 290  PRINT N(I);: HTAB 13: PRINT C(I)
 300  NEXT I
 310  VTAB 22: INPUT "ENTER ID # ";N2
 315  REM  *** SEARCH POINTER FILE        
 320  FOR I = 1 TO 11
 330  IF N(I) = N2 THEN  GOTO 380
 340  NEXT I
 350  HOME : VTAB 5: PRINT "THAT NUMBER IS NOT IN THE FILE"
 360  FOR Z = 1 TO 1500: NEXT Z
 370  GOTO 250
 380  REM  *** PRINT INFO
 390  PRINT D$;"OPEN EMPLOY-20,L20"
 400  PRINT D$;"READ EMPLOY-20,R"C(I)
 410  INPUT N
 420  INPUT X
 430  INPUT A
 440  INPUT Y
 450  INPUT S
 460  PRINT D$;"CLOSE EMPLOY-20"
 470  HOME 
 480  PRINT "ID #            :";N
 490  PRINT "SEX CODE        :";X
 500  PRINT "AGE             :";A
 510  PRINT "YRS. OF SERVICE :";Y
 520  PRINT "SALARY          :$";S
 530  VTAB 22: PRINT "PRESS ANY KEY TO CONTINUE": GET A$: HOME : GOTO 250
