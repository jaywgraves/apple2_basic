 10  REM  *** RA-SAVE S/G
 20  REM  *** JAY GRAVES
 30  REM  *** 10-9-86
 40 D$ =  CHR$ (4): REM  * CNTRL-D
 45 N = 10
 50  DIM N$(20),G1(20),G2(20),G3(20),G4(20),G5(20),G6(20)
 60  REM  *** VARIABLE NAMES
 70  REM  * N$ = NAME
 72  REM  * N = # OF NAMES
 80  REM  * G1 >> G6 = GRADES 1 >> 6
 90  REM  *** TITLE PG
 100  HOME 
 110  VTAB 5: HTAB 3: PRINT "This program will save ten names,"
 111  PRINT "with six grades for each name, in a "
 112  PRINT "Random Access file."
 120  VTAB 22: PRINT "PRESS ANY KEY TO CONTINUE": GET A$
 130  REM  *** INPUT INFO
 140  HOME 
 150  FOR I = 1 TO N
 160  INPUT "Name: ";N$(I)
 165  PRINT 
 170  INPUT "Grade 1 ";G1(I)
 180  INPUT "Grade 2 ";G2(I)
 190  INPUT "Grade 3 ";G3(I)
 200  INPUT "Grade 4 ";G4(I)
 210  INPUT "Grade 5 ";G5(I)
 220  INPUT "Grade 6 ";G6(I)
 225  PRINT : PRINT 
 230  NEXT I
 240  REM  *** MAKE TABLE
 255  HOME 
 260  PRINT "Name";: POKE 36,10: PRINT "GR1";: POKE 36,14: PRINT "GR2";: POKE 36,18: PRINT "GR3";: POKE 36,22: PRINT "GR4";: POKE 36,26: PRINT "GR5";: POKE 36,30: PRINT "GR6";: POKE 36,35: PRINT "AVE"
 270  PRINT "=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-="
 280  FOR I = 1 TO N
 282 A(I) = (G1(I) + G2(I) + G3(I) + G4(I) + G5(I) + G6(I)) / 6
 285 A(I) =  INT (A(I) + .5)
 290  PRINT N$(I);: POKE 36,10: PRINT G1(I);: POKE 36,14: PRINT G2(I);: POKE 36,18: PRINT G3(I);: POKE 36,22: PRINT G4(I);: POKE 36,26: PRINT G5(I);: POKE 36,30: PRINT G6(I);: POKE 36,35: PRINT A(I)
 300  NEXT I
 310  VTAB 22: PRINT "PRESS ANY KEY TO CONTINUE": GET A$: HOME 
 320  REM   *** SAVE FILE - 'GRADES-28'  
 325  PRINT 
 330  PRINT D$;"OPEN GRADES-28,L28"
 340  PRINT D$;"DELETE GRADES-28"
 350  PRINT D$;"OPEN GRADES-28,L28"
 360  PRINT D$;"WRITE GRADES-28,R0"
 370  PRINT N
 380  FOR I = 1 TO N
 385  PRINT D$;"WRITE GRADES-28,R"I
 390  PRINT N$(I)
 400  PRINT G1(I)
 410  PRINT G2(I)
 420  PRINT G3(I)
 430  PRINT G4(I)
 440  PRINT G5(I)
 450  PRINT G6(I)
 460  NEXT I
 470  PRINT D$;"CLOSE GRADES-28"
 480  REM  *** END
 500  HOME : END 
