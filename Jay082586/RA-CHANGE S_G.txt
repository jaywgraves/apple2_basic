 10  REM  *** RA-CHANGE S/G
 20  REM  *** JAY GRAVES
 30  REM  *** 10-20-86
 40  HOME 
 50 D$ =  CHR$ (4)
 60  DIM N$(25),G1(25),G2(25),G3(25),G4(25),G5(25),G6(25)
 70  REM  *** VARIABLES
 80  REM  * N$ = NAME
 90  REM  * N = # OF NAMES
 100  REM  * G1 -THRU- G6 =
 110  REM  * GRADES 1 -THRU- 6
 120  REM 
 130  REM   *** RET GRADES-28    
 140  PRINT 
 150  PRINT D$;"OPEN GRADES-28,L28"
 160  PRINT D$;"READ GRADES-28,R0"
 170  INPUT N
 180  FOR I = 1 TO N
 190  PRINT D$;"READ GRADES-28,R"I
 200  INPUT N$(I)
 210  INPUT G1(I)
 220  INPUT G2(I)
 230  INPUT G3(I)
 240  INPUT G4(I)
 250  INPUT G5(I)
 260  INPUT G6(I)
 270  NEXT I
 280  PRINT D$;"CLOSE GRADES-28"
 290  PRINT 
 300  REM  
 310  REM  *** CHOICES ***
 320  HOME 
 330  VTAB 5: PRINT "What would you like to do? "
 340  VTAB 10: HTAB 10: PRINT "1) Add to the file"
 350  VTAB 12: HTAB 10: PRINT "2) Delete a record"
 360  VTAB 14: HTAB 10: PRINT "3) List the file"
 370  VTAB 16: HTAB 10: PRINT "4) Terminate program"
 380  VTAB 20: HTAB 5: PRINT "Enter number of your choice";: GET X
 390  IF X < 1 OR X > 4 THEN 380
 400  ON X GOSUB 420,580,810,930
 410  GOTO 320
 420  REM  *** ADD A RECORD
 430  HOME :A = 0
 440  VTAB 5: INPUT "HOW MANY RECORDS DO YOU WISH TO ADD ";A
 450  HOME 
 460  FOR I = N + 1 TO N + A
 470  INPUT "NAME ";N$(I)
 480  PRINT : INPUT "GRADE 1 ";G1(I)
 490  INPUT "GRADE 2 ";G2(I)
 500  INPUT "GRADE 3 ";G3(I)
 510  INPUT "GRADE 4 ";G4(I)
 520  INPUT "GRADE 5 ";G5(I)
 530  INPUT "GRADE 6 ";G6(I)
 540  PRINT : PRINT 
 550  NEXT I
 560 N = N + A
 570  RETURN 
 580  REM  *** DELETE A RECORD
 590  HOME 
 600  VTAB 5
 610  INPUT "WHICH RECORD DO YOU WISH TO DELETE ";D
 620  IF D > N THEN 640
 630  GOTO 660
 640  HOME : VTAB 12: PRINT "NO SUCH RECORD": FOR Q = 1 TO 2000: NEXT Q
 650  GOTO 310
 660  HOME 
 670  IF N$(D) = "DELETE" THEN 690
 680  GOTO 720
 690  VTAB 5: PRINT "THIS RECORD HAS BEEN DELETED. PLEASE"
 700  PRINT "CHOOSE A DIFFERENT RECORD."
 710  FOR Q = 1 TO 2000: NEXT Q: GOTO 580
 720  HOME 
 730  VTAB 5: PRINT "THIS RECORD CONTAINS THE INFORMATION "
 740  PRINT "FOR ";N$(D);".  DO YOU STILL WISH"
 750  PRINT "TO DELETE THIS FILE"
 760  VTAB 12: INPUT "ENTER (Y/N) ";Q$
 770  IF Q$ = "N" THEN 310
 780 N$(D) = "DELETE"
 790 G1(D) = 0:G2(D) = 0:G3(D) = 0:G4(D) = 0:G5(D) = 0:G6(D) = 0
 800  RETURN 
 810  REM  *** LIST CONTENTS
 820  HOME 
 830  PRINT "Name";: POKE 36,10: PRINT "GR1";: POKE 36,14: PRINT "GR2";: POKE 36,18: PRINT "GR3";: POKE 36,22: PRINT "GR4";: POKE 36,26: PRINT "GR5";: POKE 36,30: PRINT "GR6";: POKE 36,35: PRINT "AVE"
 840  PRINT "---------------------------------------"
 850  FOR I = 1 TO N
 860  IF N$(I) = "DELETE" THEN 900
 870 A = (G1(I) + G2(I) + G3(I) + G4(I) + G5(I) + G6(I)) / 6
 880 A =  INT (A + .5)
 890  PRINT N$(I);: POKE 36,10: PRINT G1(I);: POKE 36,14: PRINT G2(I);: POKE 36,18: PRINT G3(I);: POKE 36,22: PRINT G4(I);: POKE 36,26: PRINT G5(I);: POKE 36,30: PRINT G6(I);: POKE 36,35: PRINT A
 900  NEXT I
 910  VTAB 22: PRINT "PRESS ANY KEY TO CONTINUE": GET A$
 920  RETURN 
 930  REM  *** SAVE FILE - 'GRADES-28'
 940  REM  *** & EXECUTE TERMINATION SEQUENCE
 950  HOME 
 960  VTAB 12: PRINT "SAVING FILE: GRADES-28"
 970  PRINT D$;"OPEN GRADES-28,L28"
 980  PRINT D$;"WRITE GRADES-28,R0"
 990  PRINT N
 1000  FOR I = 1 TO N
 1010  PRINT D$;"WRITE GRADES-28,R"I
 1020  PRINT N$(I)
 1030  PRINT G1(I)
 1040  PRINT G2(I)
 1050  PRINT G3(I)
 1060  PRINT G4(I)
 1070  PRINT G5(I)
 1080  PRINT G6(I)
 1090  NEXT I
 1100  PRINT D$;"CLOSE GRADES-28"
 1110  HOME : END 
